#version 400 core

in vec2 pass_textureCoords;
in vec3 surfaceNormal;
in vec3 toLightVector[4];
in vec3 toCameraVector;
in float visibility;

out vec4 out_Color;

uniform sampler2D modelTexture;
uniform vec3 lightColour[4];
uniform float shineDamper;
uniform float reflectivity;
uniform vec3 skyColour;

void main(void) {

    vec3 unitSurfaceNormal = normalize(surfaceNormal);
    vec3 unitCameraVector = normalize(toCameraVector);

    vec3 diffuse = vec3(0.0);
    vec3 finalSpecular = vec3(0.0);
    for(int x = 0; x < 4; x ++) {
        vec3 unitLightVector = normalize(toLightVector[x]);
        float brightness = max(dot(unitSurfaceNormal, unitLightVector), 0.0);
        vec3 lightDirection = - unitLightVector;
        vec3 reflectedLightDirection = reflect(lightDirection, unitSurfaceNormal);
        float specularFactor = max(dot(reflectedLightDirection, unitCameraVector), 0.0);
        float dampedFactor = pow(specularFactor, shineDamper);
        diffuse = diffuse + (brightness * lightColour[x]);
        finalSpecular = finalSpecular + (dampedFactor * reflectivity * lightColour[x]);
    }
    diffuse = max(diffuse, 0.2);

    vec4 textureColour = texture(modelTexture, pass_textureCoords);
    if(textureColour.a < 0.5) {discard;}

    out_Color = vec4(diffuse, 1) * textureColour + vec4(finalSpecular, 1.0);
    out_Color = mix(vec4(skyColour, 1.0), out_Color, visibility);

}