#version 400 core

in vec2 pass_textureCoords;
in vec3 surfaceNormal;
in vec3 toLightVector[4];
in vec3 toCameraVector;
in float visibility;

out vec4 out_Color;

uniform sampler2D terrainBackground;
uniform sampler2D terrainColourR;
uniform sampler2D terrainColourG;
uniform sampler2D terrainColourB;
uniform sampler2D terrainBlendMap;
uniform vec3 lightColour[4];
uniform vec3 attenuation[4];
uniform float shineDamper;
uniform float reflectivity;
uniform vec3 skyColour;

void main(void) {

    vec4 blendMapColour = texture(terrainBlendMap, pass_textureCoords);
    float backgroundAmount = 1 - (blendMapColour.r + blendMapColour.g + blendMapColour.b);
    vec2 tiledCoords = pass_textureCoords * 40.0;
    vec4 textureBackground = texture(terrainBackground, tiledCoords) * backgroundAmount;
    vec4 textureColourR = texture(terrainColourR, tiledCoords) * blendMapColour.r;
    vec4 textureColourG = texture(terrainColourG, tiledCoords) * blendMapColour.g;
    vec4 textureColourB = texture(terrainColourB, tiledCoords) * blendMapColour.b;
    vec4 textureColour = textureBackground + textureColourR + textureColourG + textureColourB;

    vec3 unitSurfaceNormal = normalize(surfaceNormal);
    vec3 unitCameraVector = normalize(toCameraVector);

    vec3 diffuse = vec3(0.0);
    vec3 finalSpecular = vec3(0.0);
    for(int x = 0; x < 4; x ++) {
        float distanceToSource = length(toLightVector[x]);
        float attenuationFactor = attenuation[x].x + (attenuation[x].y * distanceToSource) + (attenuation[x].z * distanceToSource * distanceToSource);
        vec3 unitLightVector = normalize(toLightVector[x]);
        float brightness = max(dot(unitSurfaceNormal, unitLightVector), 0.0);
        vec3 lightDirection = - unitLightVector;
        vec3 reflectedLightDirection = reflect(lightDirection, unitSurfaceNormal);
        float specularFactor = max(dot(reflectedLightDirection, unitCameraVector), 0.0);
        float dampedFactor = pow(specularFactor, shineDamper);
        diffuse = diffuse + ((brightness * lightColour[x]) / attenuationFactor);
        finalSpecular = finalSpecular + ((dampedFactor * reflectivity * lightColour[x]) / attenuationFactor);
    }
    diffuse = max(diffuse, 0.2);

    out_Color = vec4(diffuse, 1) * textureColour + vec4(finalSpecular, 1.0);
    out_Color = mix(vec4(skyColour, 1.0), out_Color, visibility);

}